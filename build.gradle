plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.3.21"
    id "idea"
    id "maven"
    id 'java-library'
    id 'maven-publish'
}

version = '2019-4.2.2'

sourceSets {
    main.kotlin.srcDirs += 'src/'
    main.java.srcDirs += 'src/'
}

allprojects {
    repositories {
        mavenCentral()
        maven {
            url "http://first.wpi.edu/FRC/roborio/maven/release"
        }
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.21"
    compileOnly "edu.wpi.first.wpilibj:wpilibj-java:2019.3.1"
    compileOnly "edu.wpi.first.ntcore:ntcore-java:2019.3.1"
    compileOnly "edu.wpi.first.wpiutil:wpiutil-java:2019.3.1"
    compileOnly "edu.wpi.first.hal:hal-java:2019.3.1"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1'
}

wrapper {
    gradleVersion = '5.2.1'
}

jar {
    manifest {
        attributes 'Implementation-Title': '4322Utils',
                'Implementation-Version': version
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}


task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.github.Nico1999'
            artifactId = '4322Utils'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}